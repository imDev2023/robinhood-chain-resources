// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";

/**
 * @title BondingConfig
 * @notice Configuration contract for BondingV5 multi-chain launch modes
 * @dev Stores configurable parameters for different launch modes.
 *      gradThreshold is calculated per-token based on airdropBips and needAcf.
 *      project60days is handled by backend (not a separate launch mode).
 */
contract BondingConfig is Initializable, OwnableUpgradeable {
    // Launch mode constants (only 3 modes)
    uint8 public constant LAUNCH_MODE_NORMAL = 0;
    uint8 public constant LAUNCH_MODE_X_LAUNCH = 1;
    uint8 public constant LAUNCH_MODE_ACP_SKILL = 2;

    // Reserve supply parameters struct (in bips, 1 bip = 0.01%, e.g., 5500 = 55.00%)
    struct ReserveSupplyParams {
        uint16 maxAirdropBips; // Maximum airdrop (e.g., 500 = 5.00%)
        uint16 maxTotalReservedBips; // At least (100% - this) must remain in bonding curve (e.g., 5500 = 55.00%)
        uint16 acfReservedBips; // ACF operations reserve (e.g., 5000 = 50.00%)
    }
    ReserveSupplyParams public reserveSupplyParams;

    // Anti-sniper tax type constants
    // Buy-side: duration over which buy anti-sniper tax decreases from 99% to 0%
    uint8 public constant ANTI_SNIPER_NONE = 0; // No anti-sniper tax (0 seconds)
    uint8 public constant ANTI_SNIPER_60S = 1; // 60 seconds buy-only (default)
    uint8 public constant ANTI_SNIPER_98M = 2; // 98 minutes buy-only
    uint8 public constant ANTI_SNIPER_98M_SELL = 3; // 98 minutes sell-only
    uint8 public constant ANTI_SNIPER_98M_BOTH = 4; // 98 minutes buy and sell
    uint8 public constant ANTI_SNIPER_10M = 5; // 10 minutes buy-only

    // Scheduled launch parameters
    struct ScheduledLaunchParams {
        uint256 startTimeDelay; // Time delay for scheduled launches (e.g., 24 hours)
        uint256 normalLaunchFee; // Fee for scheduled launches / marketing (e.g., 100 VIRTUAL)
        uint256 acfFee; // Extra fee when needAcf = true (e.g., 10 VIRTUAL on base, 150 VIRTUAL on eth)
    }
    // public for Etherscan visibility; use getScheduledLaunchParams() for memory struct
    ScheduledLaunchParams public scheduledLaunchParams;

    // Global wallet to receive reserved tokens (airdrop + ACF)
    address public teamTokenReservedWallet;

    /// @notice Backend allowlist (BondingV5): preLaunch for X_LAUNCH & ACP_SKILL; launch() for X_LAUNCH, ACP_SKILL, or isProject60days (taxRecipient must be set by backend before launch)
    mapping(address => bool) public isPrivilegedLauncher;

    // Common bonding curve parameters (unified across all launch modes)
    struct BondingCurveParams {
        uint256 fakeInitialVirtualLiq; // Normal launches (e.g., 6300 * 1e18)
        uint256 targetRealVirtual; // Target VIRTUAL from users at graduation (e.g., 42000 * 1e18)
    }
    BondingCurveParams public bondingCurveParams;

    struct Data {
        address token;
        string name;
        string _name;
        string ticker;
        uint256 supply;
        uint256 price;
        uint256 marketCap;
        uint256 liquidity;
        uint256 volume;
        uint256 volume24H;
        uint256 prevPrice;
        uint256 lastUpdated;
    }

    struct Token {
        address creator;
        address token;
        address pair;
        address agentToken;
        Data data;
        string description;
        uint8[] cores;
        string image;
        string twitter;
        string telegram;
        string youtube;
        string website;
        bool trading;
        bool tradingOnUniswap;
        uint256 applicationId;
        uint256 initialPurchase;
        uint256 virtualId;
        bool launchExecuted;
    }

    // V5 configurable launch parameters (stored separately per token)
    struct LaunchParams {
        uint8 launchMode;
        uint16 airdropBips; // in bips, 1 bip = 0.01% (e.g., 234 = 2.34%)
        bool needAcf;
        uint8 antiSniperTaxType;
        bool isProject60days;
    }

    // Common deploy parameters shared across all modes
    struct DeployParams {
        bytes32 tbaSalt;
        address tbaImplementation;
        uint32 daoVotingPeriod;
        uint256 daoThreshold;
    }
    // public for Etherscan visibility; use getDeployParams() for memory struct
    DeployParams public deployParams;

    // Common parameters
    uint256 public initialSupply;
    address public feeTo;

    /// @dev Appended in upgrade v2 — do not insert before `feeTo` (would shift prior slots).
    /// Fake initial VIRTUAL for needAcf launches (e.g., 14000 * 1e18). Normal launches use
    /// `bondingCurveParams.fakeInitialVirtualLiq`.
    uint256 public acfFakeInitialVirtualLiq;

    /// @dev Appended in upgrade v2 — receives bonding-curve excess tokens at graduation to be burned.
    address public graduationExcessBurnWallet;

    /// @dev Fake virtual liq for pre-upgrade tokens at graduation (BondingV5 mapping unset). Prod default: 6300.
    uint256 public legacyInitialVirtualLiq;

    event DeployParamsUpdated(DeployParams params);
    event TeamTokenReservedWalletUpdated(address wallet);
    event GraduationExcessBurnWalletUpdated(address wallet);
    event LegacyInitialVirtualLiqUpdated(uint256 legacyInitialVirtualLiq);
    event CommonParamsUpdated(uint256 initialSupply, address feeTo);
    event BondingCurveParamsUpdated(BondingCurveParams params);
    event AcfFakeInitialVirtualLiqUpdated(uint256 acfFakeInitialVirtualLiq);
    event ReserveSupplyParamsUpdated(ReserveSupplyParams params);
    event PrivilegedLauncherUpdated(address indexed launcher, bool allowed);

    error InvalidAntiSniperType();
    error InvalidReserveBips();
    error AirdropBipsExceedsMax();

    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }

    function initialize(
        uint256 initialSupply_,
        address feeTo_,
        address teamTokenReservedWallet_,
        ReserveSupplyParams memory reserveSupplyParams_,
        ScheduledLaunchParams memory scheduledLaunchParams_,
        DeployParams memory deployParams_,
        BondingCurveParams memory bondingCurveParams_,
        uint256 acfFakeInitialVirtualLiq_
    ) external initializer {
        __Ownable_init(msg.sender);

        initialSupply = initialSupply_;
        feeTo = feeTo_;
        teamTokenReservedWallet = teamTokenReservedWallet_;
        reserveSupplyParams = reserveSupplyParams_;
        scheduledLaunchParams = scheduledLaunchParams_;
        deployParams = deployParams_;
        bondingCurveParams = bondingCurveParams_;
        require(acfFakeInitialVirtualLiq_ > 0, "acfFakeInitialVirtualLiq cannot be zero");
        acfFakeInitialVirtualLiq = acfFakeInitialVirtualLiq_;
    }

    /**
     * @notice Set wallet that receives excess bonding-curve tokens at graduation (to be burned off-chain).
     */
    function setGraduationExcessBurnWallet(
        address wallet_
    ) external onlyOwner {
        graduationExcessBurnWallet = wallet_;
        emit GraduationExcessBurnWalletUpdated(wallet_);
    }

    /**
     * @notice Set deploy parameters
     * @param params_ The deploy parameters
     */
    function setDeployParams(DeployParams memory params_) external onlyOwner {
        deployParams = params_;
        emit DeployParamsUpdated(params_);
    }

    /**
     * @notice Set common parameters
     * @param initialSupply_ Initial token supply
     * @param feeTo_ Address to receive fees
     */
    function setCommonParams(
        uint256 initialSupply_,
        address feeTo_
    ) external onlyOwner {
        initialSupply = initialSupply_;
        feeTo = feeTo_;
        emit CommonParamsUpdated(initialSupply_, feeTo_);
    }

    /**
     * @notice Set bonding curve parameters
     * @param params_ The bonding curve parameters (K, assetRate, targetRealVirtual)
     */
    function setBondingCurveParams(
        BondingCurveParams memory params_,
        uint256 acfFakeInitialVirtualLiq_
    ) external onlyOwner {
        require(params_.fakeInitialVirtualLiq > 0, "fakeInitialVirtualLiq cannot be zero");
        require(params_.targetRealVirtual > 0, "targetRealVirtual cannot be zero");
        require(
            acfFakeInitialVirtualLiq_ > 0,
            "acfFakeInitialVirtualLiq cannot be zero"
        );

        bondingCurveParams = params_;
        acfFakeInitialVirtualLiq = acfFakeInitialVirtualLiq_;
        emit BondingCurveParamsUpdated(params_);
        emit AcfFakeInitialVirtualLiqUpdated(acfFakeInitialVirtualLiq_);
    }

    /**
     * @notice Fake virtual liq for pre-upgrade tokens at graduation migration.
     * @dev Pre-upgrade tokens have no `tokenFakeInitialVirtualLiq` in BondingV5 (prod default: 6300).
     */
    function setLegacyInitialVirtualLiq(
        uint256 legacyInitialVirtualLiq_
    ) external onlyOwner {
        require(
            legacyInitialVirtualLiq_ > 0,
            "legacyInitialVirtualLiq cannot be zero"
        );
        legacyInitialVirtualLiq = legacyInitialVirtualLiq_;
        emit LegacyInitialVirtualLiqUpdated(legacyInitialVirtualLiq_);
    }

    /**
     * @notice Calculate bonding curve supply with validation
     * @dev Validates:
     *      1. airdropBips_ <= reserveSupplyParams.maxAirdropBips
     *      2. airdropBips + (needAcf ? acfReservedBips : 0) <= maxTotalReservedBips
     *      All values are in bips (1 bip = 0.01%, e.g., 234 = 2.34%)
     * @param airdropBips_ Airdrop in bips (e.g., 234 = 2.34%)
     * @param needAcf_ Whether ACF operations are needed (adds acfReservedBips reserve)
     * @return bondingCurveSupply The supply available for bonding curve (in base units, not wei)
     */
    function calculateBondingCurveSupply(
        uint16 airdropBips_,
        bool needAcf_
    ) external view returns (uint256) {
        if (airdropBips_ > reserveSupplyParams.maxAirdropBips) {
            revert AirdropBipsExceedsMax();
        }
        uint16 totalReserved = airdropBips_ +
            (needAcf_ ? reserveSupplyParams.acfReservedBips : 0);
        if (totalReserved > reserveSupplyParams.maxTotalReservedBips) {
            revert InvalidReserveBips();
        }
        return (initialSupply * (10000 - totalReserved)) / 10000;
    }

    /**
     * @notice Get fake initial virtual liquidity for normal launches
     * @return fakeInitialVirtualLiq The fake initial VIRTUAL liquidity in wei
     */
    function getFakeInitialVirtualLiq() external view returns (uint256) {
        return bondingCurveParams.fakeInitialVirtualLiq;
    }

    /**
     * @notice Get fake initial virtual liquidity by launch type
     * @param needAcf_ Whether the token launch requires ACF reserve
     * @return fakeInitialVirtualLiq The fake initial VIRTUAL liquidity in wei
     */
    function getFakeInitialVirtualLiq(
        bool needAcf_
    ) external view returns (uint256) {
        return needAcf_
            ? acfFakeInitialVirtualLiq
            : bondingCurveParams.fakeInitialVirtualLiq;
    }

    /**
     * @notice Fake virtual liq for pre-upgrade tokens at graduation (BondingV5 mapping unset).
     */
    function getLegacyInitialVirtualLiq() external view returns (uint256) {
        return legacyInitialVirtualLiq;
    }

    /**
     * @notice Calculate graduation threshold for a token
     * @dev Formula: gradThreshold = y0 * x0 / (targetRealVirtual + y0)
     * @param bondingCurveSupplyWei_ Bonding curve supply in wei
     * @param needAcf_ Whether the token launch requires ACF reserve
     * @return gradThreshold The graduation threshold (agent token amount in wei)
     */
    function calculateGradThreshold(
        uint256 bondingCurveSupplyWei_,
        bool needAcf_
    ) external view returns (uint256) {
        uint256 fakeInitialVirtualLiq = needAcf_
            ? acfFakeInitialVirtualLiq
            : bondingCurveParams.fakeInitialVirtualLiq;
        return
            (fakeInitialVirtualLiq * bondingCurveSupplyWei_) /
            (bondingCurveParams.targetRealVirtual + fakeInitialVirtualLiq);
    }

    /**
     * @notice Update scheduled launch parameters
     * @param params_ The new scheduled launch parameters (startTimeDelay, fee)
     */
    function setScheduledLaunchParams(
        ScheduledLaunchParams memory params_
    ) external onlyOwner {
        scheduledLaunchParams = params_;
    }

    /**
     * @notice Update team token reserved wallet
     * @param wallet_ The team token reserved wallet
     */
    function setTeamTokenReservedWallet(address wallet_) external onlyOwner {
        teamTokenReservedWallet = wallet_;
        emit TeamTokenReservedWalletUpdated(wallet_);
    }

    /**
     * @notice Authorize or revoke privileged backend wallets for:
     *         - preLaunch when launch mode is X_LAUNCH or ACP_SKILL (BondingV5)
     *         - launch() when token is X_LAUNCH, ACP_SKILL, or Project60days (BondingV5), so taxRecipient
     *           can be updated by the backend before trading starts
     */
    function setPrivilegedLauncher(address launcher_, bool allowed_) external onlyOwner {
        isPrivilegedLauncher[launcher_] = allowed_;
        emit PrivilegedLauncherUpdated(launcher_, allowed_);
    }

    /**
     * @notice Get target real VIRTUAL value
     * @return targetRealVirtual value from bondingCurveParams
     */
    function getTargetRealVirtual() external view returns (uint256) {
        return bondingCurveParams.targetRealVirtual;
    }

    /**
     * @notice Set reserve supply parameters
     * @param params_ The reserve supply parameters (all in bips, 1 bip = 0.01%, e.g., 500 = 5.00%)
     */
    function setReserveSupplyParams(
        ReserveSupplyParams memory params_
    ) external onlyOwner {
        require(params_.maxAirdropBips + params_.acfReservedBips <= params_.maxTotalReservedBips, InvalidReserveBips());
        require(params_.maxAirdropBips <= 10000, InvalidReserveBips());
        require(params_.maxTotalReservedBips <= 10000, InvalidReserveBips());
        require(params_.acfReservedBips <= 10000, InvalidReserveBips());
        
        reserveSupplyParams = params_;
        emit ReserveSupplyParamsUpdated(params_);
    }

    /**
     * @notice Check if anti-sniper tax type is valid
     * @param antiSniperType_ 0=none, 1=60s buy, 2=98m buy, 3=98m sell, 4=98m both, 5=10m buy
     * @return Whether the type is valid
     */
    function isValidAntiSniperType(
        uint8 antiSniperType_
    ) external pure returns (bool) {
        return
            antiSniperType_ == ANTI_SNIPER_NONE ||
            antiSniperType_ == ANTI_SNIPER_60S ||
            antiSniperType_ == ANTI_SNIPER_98M ||
            antiSniperType_ == ANTI_SNIPER_98M_SELL ||
            antiSniperType_ == ANTI_SNIPER_98M_BOTH ||
            antiSniperType_ == ANTI_SNIPER_10M;
    }

    /**
     * @notice Whether anti-sniper tax applies on buys for this type
     */
    function appliesAntiSniperOnBuy(
        uint8 antiSniperType_
    ) external pure returns (bool) {
        return
            antiSniperType_ == ANTI_SNIPER_60S ||
            antiSniperType_ == ANTI_SNIPER_98M ||
            antiSniperType_ == ANTI_SNIPER_98M_BOTH ||
            antiSniperType_ == ANTI_SNIPER_10M;
    }

    /**
     * @notice Whether anti-sniper tax applies on sells for this type
     */
    function appliesAntiSniperOnSell(
        uint8 antiSniperType_
    ) external pure returns (bool) {
        return
            antiSniperType_ == ANTI_SNIPER_98M_SELL ||
            antiSniperType_ == ANTI_SNIPER_98M_BOTH;
    }

    /**
     * @notice Get anti-sniper tax duration in seconds for a given type
     * @param antiSniperType_ The anti-sniper type
     * @return Duration in seconds (0 for NONE, 60 for 60S, 600 for 10M, 5880 for 98M variants)
     */
    function getAntiSniperDuration(
        uint8 antiSniperType_
    ) external pure returns (uint256) {
        if (antiSniperType_ == ANTI_SNIPER_NONE) {
            return 0;
        } else if (antiSniperType_ == ANTI_SNIPER_60S) {
            return 60; // 60 seconds
        } else if (antiSniperType_ == ANTI_SNIPER_10M) {
            return 600; // 10 minutes = 10 * 60 = 600 seconds
        } else if (
            antiSniperType_ == ANTI_SNIPER_98M ||
            antiSniperType_ == ANTI_SNIPER_98M_SELL ||
            antiSniperType_ == ANTI_SNIPER_98M_BOTH
        ) {
            return 5880; // 98 minutes = 98 * 60 = 5880 seconds
        }
        revert InvalidAntiSniperType();
    }

    /**
     * @notice Get scheduled launch params as memory struct (for contract calls)
     */
    function getScheduledLaunchParams()
        external
        view
        returns (ScheduledLaunchParams memory)
    {
        return scheduledLaunchParams;
    }

    /**
     * @notice Get deploy params as memory struct (for contract calls)
     */
    function getDeployParams() external view returns (DeployParams memory) {
        return deployParams;
    }

    /**
     * @notice Calculate total launch fee based on launch type and ACF requirement
     * @dev Fee structure:
     *      - Immediate launch, no ACF: 0
     *      - Immediate launch, with ACF: acfFee
     *      - Scheduled launch, no ACF: normalLaunchFee
     *      - Scheduled launch, with ACF: normalLaunchFee + acfFee
     * @param isScheduledLaunch_ Whether this is a scheduled launch
     * @param needAcf_ Whether ACF operations are needed
     * @return totalFee The total fee to charge
     */
    function calculateLaunchFee(
        bool isScheduledLaunch_,
        bool needAcf_
    ) external view returns (uint256) {
        uint256 totalFee = 0;
        if (isScheduledLaunch_) {
            totalFee += scheduledLaunchParams.normalLaunchFee;
        }
        if (needAcf_) {
            totalFee += scheduledLaunchParams.acfFee;
        }
        return totalFee;
    }
}
